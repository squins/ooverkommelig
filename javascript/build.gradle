import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id "com.jfrog.bintray" version "1.7.3"
}

apply plugin: 'kotlin-platform-js'
apply plugin: 'maven-publish'

archivesBaseName = 'ooverkommelig-javascript'

repositories {
    jcenter()
    mavenCentral()
}

dependencies {
    implement project(':common')
    compile libraries.kotlin_stdlib_js
    testCompile libraries.kotlin_test_js
}

kotlin {
    experimental {
        coroutines 'error'
    }
}

task sourcesJar(type: Jar) {
    from sourceSets.main.kotlin
    classifier = 'sources'
}

artifacts {
    archives sourcesJar
}

apply from: rootProject.file('bintray-common.gradle')

task copyTestCompileLibrariesToQunitReportDir {
    def outputDirectoryPath = "${buildDir}/reports/tests/qunit/lib"
    inputs.files configurations.testCompile
    outputs.files file(outputDirectoryPath)

    doLast {
        configurations.testCompile.each { File file ->
            copy {
                includeEmptyDirs = false

                from zipTree(file.absolutePath)
                into outputDirectoryPath
                include { fileTreeElement ->
                    def path = fileTreeElement.path
                    path.endsWith(".js") && (path.startsWith("META-INF/resources/") || !path.startsWith("META-INF/"))
                }
            }
        }
    }

}

task copyTestHtmlToQunitReportDir(type: Copy) {
    from file('src/test/html')
    into "${buildDir}/reports/tests/qunit/html"
    filter(ReplaceTokens, tokens: [qunitVersion: qunitVersion])
}

test.dependsOn copyTestCompileLibrariesToQunitReportDir, copyTestHtmlToQunitReportDir
